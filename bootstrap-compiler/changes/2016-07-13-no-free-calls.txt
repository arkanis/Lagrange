Changes
=======

- Removed free function call syntax for now. This is just a bootstrap compiler,
  no need to get frustrated on one syntax aspect for to long.
- Added explicit syntax for variable definition since it's no longer covered by
  free function calls.
- Added the return statement again. Forgot that in the last grammar.


Grammar
=======

module  = [ def ]
def     = "func" ID [ def-mod ] "{" [ stmt ] "}"
def-mod = ( "in" | "out" )  "(" ID ID? [ "," ID ID? ] ")"

// cexpr short for chainable expression: expr with known end and no operators
// To be more exact: No user defined operators that can be an ID. In theory we
// could put all operators with explicit tokens into cexpr.
cexpr = ID
        INTL
        STRL
        "(" expr ")"
        unary_op cexpr
        cexpr "(" ( expr [ "," expr ] )? ")"
        cexpr "[" ( expr [ "," expr ] )? "]"
        cexpr "." ID
expr  = cexpr [ binary_op cexpr ] try-only: eos
        cexpr try-only: eos

unary_op  = + - not ~
binary_op = ID * / %  + - << >> < <= > >= == != & ^ | and or = *= /= %= += -= <<= >>= &= ^= |=
  // since "while" and "if" are keywords the ID doesn't match the modifier
  // syntax for the while and if statements.


block   = "{"  [ stmt ] "}"
          "do" [ stmt ] "end"

stmt = block
       "while" expr "do" [ stmt ] "end"
                    "{"  [ stmt ] "}"
                    WSNL [ stmt ] "end"  // check as last alternative, see note 1
       "if" expr "do" [ stmt ]     ( "else"     [ stmt ] )? "end"
                 "{"  [ stmt ] "}" ( "else" "{" [ stmt ] "}" )?
                 WSNL [ stmt ]     ( "else"     [ stmt ] )? "end"  // check as last alternative, see note 1
       "return" ( expr ["," expr] )? eos
       cexpr ID ( "=" expr )? [ "," ID ( "=" expr )? ] eos  // how to differ between ID and binary_op, see note 2
       cexpr [ binary_op cexpr ] "while" expr eos
                                 "if"    expr eos
                                 eos

eos  = try-only: EOF    // don't consume because parser function consumes the EOF
       ";"
       try-only: "}"    // because of block and stmt bodies
       try-only: "end"  // because of block and stmt bodies
       try-only: "else" // because of end of true case of if statement
       WSNL             // because of if and while conditions with optional "do"
                        // try last to avoid effects like in note 1


Note 1: WSNL check on while and if statements
=============================================

When we generally ignore or accept a WSNL for a position we get some unexpected
results when the block of a while or if statement starts in the next line:

  while a < 10
  {
    a += 1
  }

This must not be parsed as a while statement where the first node of the body is
a new scope and the "end" is missing:

  while a < 10
    {
      a += 1
    }
  end  // missing!

CONSEQUENCE: First check for "do" and "{" ignoring new lines. Only when they're
not found check for an WSNL.


Note 2: How to differ between ID and binary_op
==============================================

After the first cexpr in the statement we need to differ between a variable
definition and a binary operator or eos directly after the first cexpr.

We can manage this with a two token look ahead after the first cexpr:

  ID
    "="           → var def
    ","           → var def
    try_eos()     → var def
    else          → [ binary_op cexpr ]
  binary_op_token → [ binary_op cexpr ]
  "while"         → while modifier
  "if"            → if modifier
  try_eos()       → expr only stmt

Only case that's ambiguous is if cexpr is followed by an ID. All other tokens
follow the expr statement branch. 

Or in rules:

- Token after initial cexpr not an ID?  → has to be binary_op or eos
- Token after ID is an "=", "," or eos? → statement is var definition

Best use a try_eos() function to check for all possible tokens of an eos.